// vim: ft=groovy:sw=2:ts=2:expandtab

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'nexus'
apply plugin: 'release'

group = 'co.tomlee.guava.services'

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    classpath 'co.tomlee.gradle.plugins:gradle-release-plugin:0.0.4'
  }
}

repositories {
  mavenCentral()
}

configurations {
  provided
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.7'
  compile 'com.google.guava:guava:17.0'
  compile 'org.eclipse.jetty:jetty-servlet:9.2.1.v20140609'

  provided 'javax.inject:javax.inject:1'
  provided 'org.slf4j:slf4j-log4j12:1.7.7'

  testCompile 'junit:junit:4.11'
  testCompile 'org.apache.httpcomponents:httpclient:4.3.4'
  testCompile 'org.apache.commons:commons-io:1.3.2'
}

sourceSets {
  main.compileClasspath += configurations.provided
  test.compileClasspath += configurations.provided
  test.runtimeClasspath += configurations.provided
}

idea.module {
  scopes.PROVIDED.plus += configurations.provided
}

idea.project {
  ipr.withXml { doc ->
    doc.asNode().component.find { it.@name == 'CompilerConfiguration' }['annotationProcessing'][0].replaceNode {
      annotationProcessing {
        profile(default: true, name: 'Default', useClasspath: 'true', enabled: true)
      }
    }
  }
}

modifyPom {
  project {
    name 'Guava Jetty Service'
    description 'A Guava Jetty Service'
    url 'https://github.com/thomaslee/guava-jetty-service'
    inceptionYear '2014'

    scm {
      url 'https://github.com/thomaslee/gradle-jetty-plugin'
      connection 'scm:git://github.com/thomaslee/gradle-jetty-plugin.git'
      developerConnection 'scm:git@github.com:thomaslee/gradle-jetty-plugin.git'
    }

    licenses {
      license {
        name 'The MIT License'
        url 'http://opensource.org/licenses/MIT'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'thomaslee'
        name 'Tom Lee'
        email 'github@tomlee.co'
      }
    }
  }
}

nexus {
  sign = true
}

